#!/usr/bin/env perl

use strict;
use warnings FATAL => 'all';

use Data::Dump qw(pp);
use Getopt::Long;
use Ptero::Proxy::Workflow::Execution qw();

exit main();

sub main {
    my ($package_name, $subroutine_name) = get_options();
    validate_execution_environment();

    use_package($package_name);

    my $execution = Ptero::Proxy::Workflow::Execution->new(
        url => $ENV{PTERO_WORKFLOW_EXECUTION_URL},
    );
    my $outputs = do {
        no strict 'refs';
        &{"${package_name}::${subroutine_name}"}($execution->inputs);
    };
    if (ref($outputs) ne ref({})) {
        die sprintf("Subroutine failed to return hashref: %s::%s(%s)",
            $package_name, $subroutine_name, pp($execution->inputs));
    }

    if ($outputs) {
        # only set outputs if they're not empty.
        $execution->set_outputs($outputs);
    }

    return 0;
}

sub get_options {
    my ($package_name, $subroutine_name);

    unless (
        GetOptions("package=s" => \$package_name,
            "subroutine=s" => \$subroutine_name)
        && defined($package_name) && defined($subroutine_name)
    ) {
        print STDERR "Usage: ptero-perl-subroutine-wrapper --package <Some::Package> --subroutine <some_subroutine>\n";
        exit 1;
    }

    return ($package_name, $subroutine_name);
}

my @EXPECTED_EXECUTION_ENV_VARIABLES = qw(
    PTERO_WORKFLOW_EXECUTION_URL
    PTERO_WORKFLOW_SUBMIT_URL
);

sub validate_execution_environment {
    my $validated = 1;
    for my $env_var (@EXPECTED_EXECUTION_ENV_VARIABLES) {
        unless (defined $ENV{$env_var}) {
            printf STDERR "ptero-perl-subroutine-wrapper: Environment Variable %s must be set before method execution\n", $env_var;
            $validated = 0;
        }
    }

    exit 1 unless $validated;
    return;
}

sub use_package {
    my ($package_name) = @_;

    my $error = do {
        local $@;
        eval "use $package_name qw();";
        $@;
    };

    if ($error) {
        die "Could not use package '$package_name': $error";
    }
}
